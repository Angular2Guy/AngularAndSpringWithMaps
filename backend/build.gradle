/**
 *    Copyright 2019 Sven Loesekann
   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at
       http://www.apache.org/licenses/LICENSE-2.0
   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
 */
 plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'org.graalvm.buildtools.native' version '0.10.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.hibernate.orm' version '6.6.15.Final'
}

group = 'ch.xxx'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

repositories {
	mavenCentral()
}

hibernate {
  enhancement {
    lazyInitialization = true
    dirtyTracking = true    
  }
}

dependencies {
	implementation project(':frontend')
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'	
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'org.liquibase:liquibase-core'
	implementation 'com.graphql-java:graphql-java-extended-scalars:22.0'
	implementation 'commons-net:commons-net:3.8.0'		
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.graphql:spring-graphql-test'
	testImplementation 'com.tngtech.archunit:archunit-junit5:1.4.0'
	
	if(project.hasProperty('withAngular')) {
		implementation 'org.postgresql:postgresql'
		testImplementation 'com.h2database:h2'
	} else {
		runtimeOnly 'com.h2database:h2'
	}
}

bootJar {
   archiveFileName = 'angularAndSpringWithMaps.jar'
}

test {
	useJUnitPlatform()
}

graalvmNative {
    binaries {
        main {
          buildArgs.add("--gc=G1")
        }
    }
}

//eclpise fix
tasks.register('copyClasses') {
   doLast{
      copy {
         from layout.buildDirectory.dir("classes/java")
         include "**/*.class"
         into layout.projectDirectory.dir("bin")
      }
   }
}

task buildDockerImage {
	if(project.hasProperty('buildDockerImage')) {	
		logger.info('Task buildDockerImage')
		bootBuildImage {
			imageName = 'angular2guy/angularandspringwithmaps:latest'
			environment = ["BPL_JVM_HEAD_ROOM" : "10","SPRING_PROFILES_ACTIVE" : "prod"]
			publish = false
		}
	}
}

build.dependsOn copyClasses
